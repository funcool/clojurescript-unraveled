=== Getting Started with the Compiler

At this point, you are surely very bored with the constant theoretical explanations about
the language itself and will want to write and execute some code. The goal of this section
is to provide a little practical introduction to the _ClojureScript_ compiler.

The _ClojureScript_ compiler takes the source code that has been split over numerous
directories and namespaces and compiles it down to JavaScript. Today, JavaScript has a
great number of different environments where it can be executed - each with its own
peculiarities.

This chapter intends to explain how to use _ClojureScript_ without any additional tooling.
This will help you understand how the compiler works and how you can use it when other
tooling is not available (such as link:http://leiningen.org/[leiningen] +
link:https://github.com/emezeske/lein-cljsbuild[cljsbuild] or
link:http://boot-clj.com/[boot]).


==== Execution environments

What is an execution environment? An execution environment is an engine where JavaScript
can be executed. For example, the most popular execution environment is a browser (Chrome,
Firefox, ...) followed by the second most popular - link:https://nodejs.org/[nodejs]/
link:https://iojs.org/en/index.html[iojs].

There are others, such as Rhino (JDK 6+), Nashorn (JDK 8), QtQuick (QT),... but none of
them have significant differences from the first two. So, _ClojureScript_ at the moment
may compile code to run in the browser or in nodejs/iojs-like environments out of the box.


==== Download the compiler

The _ClojureScript_ compiler is implemented in Java. To use it, you should have jdk8
installed. _ClojureScript_ itself only requires JDK 7, but the standalone compiler that we
are going to use in this chapter requires JDK 8, which can be found at
http://www.oracle.com/technetwork/java/javase/downloads/jdk8-downloads-2133151.html

You can download the latest _ClojureScript_ compiler using `wget`:

[source, bash]
----
wget https://github.com/clojure/clojurescript/releases/download/r3308/cljs.jar
----

The _ClojureScript_ compiler is packaged in a standalone executable jar file, so this is
the only file (along with JDK 8) that you need to compile your _ClojureScript_ source code
to JavaScript.


==== Compile for nodejs/iojs

Let’s start with a practical example compiling code that will target *nodejs*/*iojs*. For
this example, you should have nodejs or iojs (recommended) installed.

There are different ways to install iojs, but the recommended way is using nvm (node
version manager). You can read the instructions on how to install and use nvm on its
link:https://github.com/creationix/nvm[home page].

Then you have installed the nvm, follow installing the latest version of the iojs:

[source, shell]
----
nvm install iojs-v2.3.4
nvm alias default iojs-v2.3.4
----

You can test if *iojs* is installed in your system with this command:

[source, shell]
----
$ iojs --version
v2.3.4
----


===== Create the example application

////
TODO: Clarify where to put cljs.jar and show project tree structure
Comment from @NathanSmutz:
Having seen linengen projects, I might guess that these go above src (or at least build.clj might) but it's not obvious. We probably want a little graphic or tree-chart showing where each file goes and probably a sidebar about project structure.
////

For the first step of our practical example, we will create our application directory
structure and populate it with example code.

Start by creating the directory tree structure for our “hello world” application:

[source, bash]
----
mkdir -p src/myapp
touch src/myapp/core.cljs
----

Second, write the example code into the previously created `src/myapp/core.cljs` file:

[source, clojure]
----
(ns myapp.core
  (:require [cljs.nodejs :as nodejs]))

(nodejs/enable-util-print!)

(defn -main
  [& args]
  (println "Hello world!"))

(set! *main-cli-fn* -main)
----

NOTE: It is very important that the declared namespace in the file exactly matches the
directory structure. This is the way _ClojureScript_ structures its source code.


===== Compile the example application

In order to compile that source code, we need a simple build script that tells the
_ClojureScript_ compiler the source directory and the output file. _ClojureScript_ has a
lot of other options, but at this moment we can ignore that.

Let’s create the _build.clj_ file with the following content:

[source, clojure]
----
(require '[cljs.build.api :as b])

(b/build "src"
 {:main 'myapp.core
  :output-to "main.js"
  :output-dir "out"
  :target :nodejs
  :verbose true})
----

This is a brief explanation of the compiler options used in this example:

* The `:output-to` parameter indicates to the compiler the destination of the compiled
  code, in this case to the "main.js" file.
* The `:main` property indicates to the compiler the namespace that will act as the entry
  point of your application when it's executed.
* The `:target` property indicates the platform where you want to execute the compiled
  code. In this case, we are going to use *iojs* (formerly nodejs). If you omit this
  parameter, the source will be compiled to run in the browser environment.

To run the compilation, just execute the following command:

[source, bash]
----
java -cp cljs.jar:src clojure.main build.clj
----

And when it finishes, execute the compiled file using *iojs*:

[source, shell]
----
$ iojs main.js
Hello world!
----


==== Compile for the Browser

In this section we are going to create an application similar to the "hello world" example
from the previous section to run in the browser environment. The minimal requirement for
this application is just a browser that can execute JavaScript.

The process is almost the same, and the directory structure is the same. The only things
that changes is the entry point of the application and the build script. So, start
re-creating the directory tree from previous example in a different directory.

Then, write new content to the `src/myapp/core.cljs` file:

[source, clojure]
----
(ns myapp.core)

(enable-console-print!)

(println "Hello world!")
----

In the browser environment we do not need a specific entry point for the application, so
the entry point is the entire namespace.


===== Compile the example application

In order to compile the source code to run properly in a browser, overwrite the
_build.clj_ file with the following content:

[source, clojure]
----
(require '[cljs.build.api :as b])

(b/build "src"
 {:output-to "main.js"
  :output-dir "out/"
  :source-map "main.js.map"
  :main 'myapp.core
  :verbose true
  :optimizations :none})
----

This is a brief explanation of the compiler options we're using:

* The `:output-to` parameter indicates to the compiler the destination of the compiled
  code, in this case the "main.js" file.
* The `:main` property indicates to the compiler the namespace that will act as the  entry
  point of your application when it's executed.
* `:source-map` indicates the destination of the source map. (The source map connects the
  ClojureScript source to the generated JavaScript so that error messages can point you
  back to the original source.)
* `:output-dir` indicates the destination directory for all file sources used in a
  compilation. It is just for making source maps work properly with the rest of the code,
  not only your source.
* `:optimizations` indicates the compilation optimization. There are different values for
  this option, but that will be covered in subsequent sections in more detail.

To run the compilation, just execute the following command:

[source, bash]
----
java -cp cljs.jar:src clojure.main build.clj
----

This process can take some time, so do not worry; wait a little bit. The JVM bootstrap
with the Clojure compiler is slightly slow. In the following sections, we will explain how
to start a watch process to avoid constantly starting and stopping this slow process.

While waiting for the compilation, let's create a dummy HTML file to make it easy to
execute our example app in the browser. Create the _index.html_ file with the following
content; it goes in the main _myapp_ directory.

[source, html]
----
<!DOCTYPE html>
<html>
  <header>
    <meta charset="utf-8" />
    <title>Hello World from ClojureScript</title>
  </header>
  <body>
    <script src="main.js"></script>
  </body>
</html>
----

Now, when the compilation finishes and you have the basic HTML file you can just open it
with your favorite browser and take a look in the development tools console. The
"Hello world!" message should appear there.


==== Watch process

Surely, you have already experienced the slow startup of the _ClojureScript_ compiler. To
solve this, the _ClojureScript_ standalone compiler also comes with tools to start a
process that watches the changes in some directory and performs an incremental
compilation.

Start creating another build script, but in this case, name it _watch.clj_:

[source, clojure]
----
(require '[cljs.build.api :as b])

(b/watch "src"
 {:output-to "main.js"
  :output-dir "out/"
  :source-map "main.js.map"
  :main 'myapp.core
  :optimizations :none})
----

Now, execute that script like any other that you have executed in previous sections:

[source, bash]
----
$ java -cp cljs.jar:src clojure.main watch.clj
Building ...
Reading analysis cache for jar:file:/home/niwi/cljsbook/playground/cljs.jar!/cljs/core.cljs
Compiling out/cljs/core.cljs
Using cached cljs.core out/cljs/core.cljs
... done. Elapsed 0.8354759 seconds
Watching paths: /home/niwi/cljsbook/playground/src

Change detected, recompiling ...
Compiling src/myapp/core.cljs
Compiling out/cljs/core.cljs
Using cached cljs.core out/cljs/core.cljs
... done. Elapsed 0.191963443 seconds
----

You can observe that in the second compilation, the time is drastically reduced. Another
advantage of this method is that it gives a little bit more output.


==== Optimization levels

The _ClojureScript_ compiler has different level of optimizations. Behind the scenes,
those compilation levels are coming from Google Closure Compiler.

A very simplified overview of the compilation process is:

1. The reader reads the code and makes some analysis. This process can raise some warnings
   during this phase.
2. Then, the _ClojureScript_ compiler emits JavaScript code. The result is one JavaScript
   file for each cljs file.
3. The generated files passes through the Closure Compiler that, depending on the
   optimization level and other options (sourcemaps, output dir output to, ...) generates
   the final output.

The final output depends strictly on the optimization level.


===== none

Implies that closure compiler just writes the files as is, without any additional
optimization applied to the source code. This optimization level is mandatory if you are
targeting *nodejs* or *iojs* and is appropiate in development mode when your code targets
the browser.


===== whitespace

This optimization level consists of concatenating the compiled files in an appropriate
order, removing line breaks and other whitespace and generating the output as one large
file.

It also has some compilation speed penalty, resulting in slower compilations. In any case,
it is not terribly slow and is completely usable in small/medium applications.


===== simple

The simple compilation level implies (includes) all transformations from whitespace
optimization and additionally performs optimizations within expressions and functions,
including renaming local variables and function parameters to shorter names.

Compilation with the `:simple` optimization always preserves the functionality of
syntactically valid JavaScript, so it does not interfere with the interaction between the
compiled _ClojureScript_ and other JavaScript.


===== advanced

TBD


=== Working with the REPL

////
TODO: maybe it would be interesting to take some ideas from
http://www.alexeberts.com/exploring-the-clojurescript-repl/
////


==== Introduction

Although you can create a source file and compile it every time you want to try something
out in ClojureScript, it's easier to use the REPL. REPL stands for:

* Read - get input from the keyboard
* Evaluate the input
* Print the result
* Loop back for more input

In other words, the REPL lets you try out ClojureScript concepts and get immediate
feedback.

_ClojureScript_ comes with support for executing the REPL in different execution
environments, each one has its own advantages or disadvantages. For example, you can run a
REPL in nodejs but in that environment you don't have access to the DOM.

In summary: everthing really depends on your needs or requirements.


==== Nashorn REPL

The Nashorn REPL is the easiest and maybe most painless REPL environment because it does
not require any special stuff, just the JVM (JDK 8) that you have used in previous
examples for running the _ClojureScript_ compiler.

Let’s start creating the _repl.clj_ file with the following content:

[source, clojure]
----
(require '[cljs.repl]
         '[cljs.repl.nashorn])

(cljs.repl/repl
 (cljs.repl.nashorn/repl-env)
 :output-dir "out"
 :cache-analysis true)
----

Then, execute the following command to get the REPL up and running:

[source, bash]
----
$ java -cp cljs.jar:src clojure.main repl.clj
To quit, type: :cljs/quit
cljs.user=> (+ 1 2)
3
----

You may have noticed that the REPL does not have support for history and other shell-like
facilities. This is because the default REPL does not comes with "readline" support. But
this problem can be solved using the simple tool called `rlwrap` that you can certainly
find with the package manager of your operating system.

The `rlwrap` makes the REPL "readline" capable and will allow you to have command history,
code navigation, and other shell-like utilities that will make your REPL experience much
more pleasant. To use it, just prepend it to the previous command that we have used to
execute the REPL:

[source, bash]
----
$ rlwrap java -cp cljs.jar:src clojure.main repl.clj
To quit, type: :cljs/quit
cljs.user=> (+ 1 2)
3
----


==== Node REPL

This REPL uses nodejs/iojs as an execution environment and obviously requires that nodejs
or iojs will be installed in your system.

Surely you are asking yourself, why do I need a repl with nodejs when we have nashorn
that does not require any additional dependencies. The answer is very simple: node/iojs
is the most used JavaScript execution environment on the backend, and it has a great
amount of community packages around it.

Well, the good notice is that start nodejs/iojs repl is very very easy once you have it
installed in your system. Start writing this content to a new `repl.clj` file:

[source, clojure]
----
(require '[cljs.repl]
         '[cljs.repl.node])

(cljs.repl/repl
 (cljs.repl.node/repl-env)
 :output-dir "out"
 :cache-analysis true)
----

And start the REPL like you have done it previously with nashorn REPL:

[source,bash]
----
$ rlwrap java -cp cljs.jar:src clojure.main repl.clj
To quit, type: :cljs/quit
cljs.user=> (+ 1 2)
3
----


==== Browser REPL

This REPL is the most laborious to get up and running. This is because it uses a browser
for its execution environment and it has additional requirements.

Let’s start by creating a file named `brepl.clj` with the following content:

[source, clojure]
----
(require
  '[cljs.build.api :as b]
  '[cljs.repl :as repl]
  '[cljs.repl.browser :as browser])

(b/build "src"
 {:output-to "main.js"
  :output-dir "out/"
  :source-map "main.js.map"
  :main 'myapp.core
  :verbose true
  :optimizations :none})

(repl/repl (browser/repl-env)
  :output-dir "out")
----

This script builds the source, just as we did earlier, and then starts the REPL.

But the browser REPL also requires that some code be executed in the browser before
getting the REPL working. To do that, just re-create the application structure very
similar to the one that we have used in previous sections:

[source, bash]
----
mkdir -p src/myapp
touch src/myapp/core.cljs
----

Then, write new content to the `src/myapp/core.cljs` file:

[source, clojure]
----
(ns myapp.core
 (:require [clojure.browser.repl :as repl]))

(defonce conn
  (repl/connect "http://localhost:9000/repl"))

(enable-console-print!)

(println "Hello, world!")
----

And finally, create the missing _index.html_ file that is going to be used as the entry
point for running the browser side code of the REPL:

[source, html]
----
<!DOCTYPE html>
<html>
  <header>
    <meta charset="utf-8" />
    <title>Hello World from ClojureScript</title>
  </header>
  <body>
    <script src="main.js"></script>
  </body>
</html>
----

Well, that was a lot of setup! But trust us, it’s all worth it when you see it in action.
To do that, just execute the `brepl.clj` in the same way that we have done it in previous
examples:

[source, bash]
----
$ rlwrap java -cp cljs.jar:src clojure.main brepl.clj
Compiling client js ...
Waiting for browser to connect ...
----

And finally, open your favourite browser and go to http://localhost:9000/. Once the page
is loaded (the page will be blank), switch back to the console where you have run the REPL
and you will see that it is up and running:

[source, bash]
----
[...]
To quit, type: :cljs/quit
cljs.user=> (+ 14 28)
42
----

One of the big advantages of this REPL is that you have access to all the browser
environment. To demonstrate it, just enter `(js/alert "hello world")` in the repl. The
result of that will be the typical browser alert dialog.


=== The Closure Library

A google closure library is a javascript library developed by google, based on a modular
architecture and provides cross-browser functions for DOM manipulations and events, ajax
and JSON, among other features.

It's written specifically to take advantage of the Closure Compiler (that is used
internally by the _ClojureScript_ compiler).

And _ClojureScript_ is built up on Closure Compiler and Closure Library. The great example
are the _ClojureScript_ namespaces, they are defined as closure modules. This means that
you can interact with closure library in a very easy way:

[source, clojure]
----
(ns yourapp.core
  (:require [goog.dom :as dom]))

(def element (dom/getElement "body"))
----

With previous snippet of code you can observe the way you can import the *dom* module of
the closure library and use one function declared in that module.

Additionally, the closure library also exposes "special" modules that behaves like a
class or object. For import this kind of things you should use the `:import` directive.

[source, clojure]
----
(ns yourapp.core
  (:import goog.History))

(def instance (History.))
----

If you are familiar with Clojure, it uses this approach for import the Java clases using
the `:import` direcrtive. However, if you define types (classes) using _ClojureScript_
primitives, you should not use `:import` for import them, the standard `:require`
directives should be used.


=== Dependency managemen

Until now, we have used the builtin _ClojureScript_ toolchain to compile our sources to
JavaScript. It is OK to start using it and to understand how it works. But it is not very
comfortable for use on big projects with dependencies to third party libraries.

This chapter intends to explain how you can use *Leiningen*, the defacto clojure build
and dependency management tool, to build _ClojureScript_ projects. There is another
build tool called *boot* that is growing in popularity, but at this momenent it will not be
covered in this book.


==== Installing leiningen

The installation process of leiningen is quite simple; just follow these steps:

[source, bash]
----
mkdir ~/bin
cd ~/bin
wget https://raw.githubusercontent.com/technomancy/leiningen/stable/bin/lein
chmod a+x ./lein
export PATH=$PATH:~/bin
----

Make sure that the `~/bin` directory is always set on your path. To make it permanent,
add the line starting with ` export` to your `~/.bashrc` file (we are supposing that you
are using the bash shell).

Now, open another clean terminal and execute `lein version`:

[source, bash]
----
$ lein version
Leiningen 2.5.1 on Java 1.8.0_45 OpenJDK 64-Bit Server VM
----

NOTE: We are supposing that you are using a Unix-like system such as Linux or BSD. If you
are a Windows user, please check the instructions on the
link:http://leiningen.org/[Leiningen homepage]. You can also get the Linux/Mac OSX/BSD
version of the leiningen script at the web site.

==== First project

The best way to show how a tool works is by creating a toy project with it. In this case
we will go to create one little application that calculates if a year is leap or not. And
for it, we will use the *mies* leiningen template.

NOTE: Templates are a way that leiningen has to provide a initial layout for an
application or a library and clojure community has a great amount of them. In this case
we'll use *mies* template that is maintained by the clojurescript core developer. If you
want to know more about templates, you should read the leiningen doc. Explaining templates
is completely out of scope of this book.

Let's start creating the project layout:

[source, bash]
----
$ lein new mies leapyears
$ cd leapyears # move into newly created project directory
----

The project has the following structure:

----
leapyears
├── index.html
├── project.clj
├── README.md
├── scripts
│   ├── build
│   ├── release
│   ├── watch
│   ├── repl
│   └── brepl
└── src
    └── leapyears
        └── core.cljs
----

The `project.clj` file contains information that Leiningen uses to download dependencies
and build the project. For now, just trust that everything in that file is exactly as it
should be.

Open the `index.html` file and add the following content at the beginning of body:

[source, html]
----
<section class="viewport">
  <div id="result">
    ----
  </div>
  <form action="" method="">
    <label for="year">Input a year</label>
    <input id="year" name="year" />
  </form>
</section>
----

And the next step is adding some code to make the form interactive. Put the following
code into the `src/leapyear/core.cljs`:

[source, clojure]
----
(ns leapyears.core
  (:require [goog.dom :as dom]
            [goog.events :as events]
            [cljs.reader :refer (read-string)]))

(enable-console-print!)

(def input (dom/getElement "year"))
(def result (dom/getElement "result"))

(defn leap?
  [year]
  (and (zero? (js-mod year 4))
       (pos? (js-mod year 100))
       (pos? (js-mod year 400))))

(defn on-change
  [event]
  (let [target (.-target event)
        value (read-string (.-value target))]
    (if (leap? value)
      (set! (.-innerHTML result) "YES")
      (set! (.-innerHTML result) "NO"))))

(events/listen input "keyup" on-change)
----

And finally, compile the clojurescript code with:

[source, bash]
----
$ ./scripts/watch
----

That behind the scenes executes this:

[source, bash]
----
rlwrap lein trampoline run -m clojure.main scripts/watch.clj
----

WARNING: you should have `rlwrap` installed on your system.

Additionally to `watch` script, the *mies* template also generates a bunch of other
scripts such as `build`,`release`,... but for our purposes in this section they are
completely useless. And probably after reading the compiler and repl sections, you already
will be familiar with almost all the scripts that comes with *mies* template.


==== Managing dependencies

The real purpose of use leiningen for the ClojureScript compilation process is managing
dependencies. Because it makes this task more obvious in comparison to doing it
manually.

The dependencies, among other parameters, are declared in the `project.clj` file and have
this form (from *mies* template):

[source, clojure]
----
(defproject leapyears "0.1.0-SNAPSHOT"
  :description "FIXME: write this!"
  :url "http://example.com/FIXME"
  :dependencies [[org.clojure/clojure "1.7.0"]
                 [org.clojure/clojurescript "0.0-3308" :classifier "aot"
                  :exclusion [org.clojure/data.json]]
                 [org.clojure/data.json "0.2.6" :classifier "aot"]]
  :jvm-opts ^:replace ["-Xmx1g" "-server"]
  :node-dependencies [[source-map-support "0.3.2"]]
  :plugins [[lein-npm "0.5.0"]]
  :source-paths ["src" "target/classes"]
  :clean-targets ["out" "release"]
  :target-path "target")
----

And here is a brief explanation of the properties relevant for ClojureScript:

* `:dependencies`: a vector of dependencies that your project needs.
* `:clean-targets`: a vector of paths that `lein clean` should delete.

The dependencies in ClojureScript are packaged using `jar` files. If you are coming from
Clojure or any JVM language, `jar` files will be very familiar to you. But if you
aren't familiar with them, do not worry: a .jar file is like a plain zip file that contains
the `project.clj` for the library, some metadata, and the ClojureScript sources. The
packaging will be explained in other section.

Clojure packages are often published on link:http://clojars.org[Clojars]; also you
can find many different third party libraries on the
link:https://github.com/clojure/clojurescript/wiki#libraries[ClojureScript Wiki].

To understand dependencies better, we are going to replace the DOM utiltiles of the Google Closure
library with a third party library for interacting with the DOM.

Let start adding `[prismatic/dommy "1.1.0"]` into the `:dependencies` in the
*project.clj*:

[source, clojure]
----
:dependencies [[org.clojure/clojure "1.7.0"]
               [org.clojure/clojurescript "0.0-3308" :classifier "aot"
                :exclusion [org.clojure/data.json]]
               [org.clojure/data.json "0.2.6" :classifier "aot"]
               [prismatic/dommy "1.1.0"]]
----

At this moment you should restart the watch process that we had started at the beginning
of this subchapter:

[source, bash]
----
$ ./scripts/watch
Retrieving prismatic/dommy/1.1.0/dommy-1.1.0.pom from clojars
Retrieving prismatic/dommy/1.1.0/dommy-1.1.0.jar from clojars
Building ...
... done. Elapsed 0.227999601 seconds
Watching paths: /home/niwi/cljsbook/playground/leapyears/src
----

You can observe that dommy dependency is successfully downloaded, and then the watch
process is started.

Now it's time to write some code. Replace the contents of the `src/leapyear/core.cljs`
with the following content:

[source, clojure]
----
(ns leapyears.core
  (:require [dommy.core :as dom]
            [cljs.reader :refer (read-string)]))

(enable-console-print!)

(def input (dom/sel1 :#year))
(def result (dom/sel1 :#result))

(defn leap?
  [year]
  (and (zero? (js-mod year 4))
       (pos? (js-mod year 100))
       (pos? (js-mod year 400))))

(defn on-change
  [event]
  (let [target (.-target event)
        value (read-string (dom/value target))]
    (if (leap? value)
      (dom/set-html! result "YES")
      (dom/set-html! result "NO"))))

(dom/listen! input :keyup on-change)
----

As you can observe, the code now looks more “Clojure-friendly.” The host
interoperabilty calls are almost hidden.

NOTE: This example does not means that we prefer `dommy` over the Google Closure library. In
fact, dommy does not have documentation, so we do not recommend use it.


=== Using javascript libraries

TBD

////
Ideas:

- include javascript as is
- include javascript libraries with modules (es6 commonjs)
- externs generation
////


=== Unit testing

There’s not a lot to say here; testing in _ClojureScript_ consists of the same concepts as
you may know from any other language such as Clojure, Java, Python, JavaScript, etc.

Regardless of the languag,e the main objective of unit testing is to run some test code
and verify that your code behaves as expected and returns without raising unexpected
exceptions.

The immutablity of _ClojureScript_ in fact helps your program to be less
error prone and facilitates testing a little bit. Another advantage of _ClojureScript_
is that it tends to use plain data instead of complex objects. Building "mock" objects
for testing is greatly simplified.


==== First steps

The "official" _ClojureScript_ testing framework is built-in on its core under the "cljs.test"
namespace. It is a very simple library, but it should be more than enough for our purposes.

There are other libraries that offer additional features or directly different approaches
for testing, such as link:https://github.com/clojure/test.check[test.check]. However, they
will not be covered in this subchapter.

Start creating a new project using the *mies* leiningen template for experimenting with
tests:

[source, bash]
----
$ lein new mies appfortesting
$ cd appfortesting
----

This project will contain the same layout as we have seen in the *dependency management*
subchapter, so we won’t explain it again.

The next step is a creating a directory tree for our tests:

[source, bash]
----
$ mkdir -p test/appfortesting
$ touch test/appfortesting/core_tests.cljs
----

Also, we should adapt the existing `watch.clj` script to work with this newly created
test directory:

[source, clojure]
----
(require '[cljs.build.api :as b])

(b/watch (b/inputs "test" "src")
  {:main 'appfortesting.core_tests
   :target :nodejs
   :output-to "out/appfortesting.js"
   :output-dir "out"
   :verbose true})
----

This new script will compile and watch both directories "src" and "test", and
sets the new entry point to the `appfortesting.core_tests` namespace.

And then, write some test code into the `core_tests.cljs` file:

[source, clojure]
----
(ns appfortesting.core-tests
  (:require [cljs.test :as t]))

(enable-console-print!)

(t/deftest my-first-test
  (t/is (= 1 2)))

(set! *main-cli-fn* #(t/run-tests))
----

The relevant part of that code snippet is:

[source, clojure]
----
(t/deftest my-first-test
  (t/is (= 1 2)))
----

The `deftest` macro is a basic primitive for define our tests. It takes a name as its first
parameter, followed of one or multiple "asserts" using the `is` macro. In that example,
the assert tries check that `(= 1 2)` is true.

Let's try to run this. First start the watch process:

[source,bash]
----
$ ./scripts/watch
Building ...
Analyzing jar:file:/home/niwi/.m2/repository/org/clojure/clojurescript/0.0-3308/clojurescript-0.0-3308-aot.jar!/cljs/core.cljs
Compiling out/cljs/core.cljs
Using cached cljs.core out/cljs/core.cljs
... done. Elapsed 3.862126827 seconds
Watching paths: /home/niwi/cljsbook/playground/appfortesting/test, /home/niwi/cljsbook/playground/appfortesting/src
----

And when the compilation is finished, try to run the compiled file with `iojs`
(or `node`):

[source, bash]
----
$ iojs out/appfortesting.js

Testing appfortesting.core-tests

FAIL in (my-first-test) (cljs/test.js:374:14)
expected: (= 1 2)
  actual: (not (= 1 2))

Ran 1 tests containing 1 assertions.
1 failures, 0 errors.
----

You can observe that expected assert failure is successfully printed in the console. To
fix the test, just change the `=` with `not=` and run again the file:

[source, bash]
----
$ iojs out/appfortesting.js

Testing appfortesting.core-tests

Ran 1 tests containing 1 assertions.
0 failures, 0 errors.
----

It is fine to test these kinds of assertions, but they are not very useful. Let's go to
test some application code. For this, we will use a function to check if a year is leap
or not. Then, write the following content to the `src/appfortesting/core.clj` file:

[source, clojure]
----
(defn leap?
  [year]
  (and (zero? (js-mod year 4))
       (pos? (js-mod year 100))
       (pos? (js-mod year 400))))
----

Next, write a new test case to check that our new function defined in the
`appfortesting.core` namespace works properly. This is the new content
of the `core_tests.cljs` file:

[source, clojure]
----
(ns appfortesting.core-tests
  (:require [cljs.test :as t]
            [appfortesting.core :as core]))

(enable-console-print!)

(t/deftest my-first-test
  (t/is (not= 1 2)))

(t/deftest my-second-test
  (t/is (core/leap? 1980))
  (t/is (not (core/leap? 1981))))

(set! *main-cli-fn* #(t/run-tests))
----

You can again run the compiled file to check that the new test passes as expected.


==== Async Testing

One of the peculiarities of _ClojureScript_ is that is built up on asynchronous, single
theaded execution environments, and this has its own challenges.

In async execution environments we should be able test asynchronous functions. So,
in this case, the _ClojureScript_ testing library offers a simply facility to do it:
the `async` macro. It allows you to create blocks that play well with asynchronous code.

First, we need to write a function that works in an asynchronous way. For this
purpose we will create the `async-leap?` predicate that will do the same operation but
return a result using a callback:

[source, clojure]
----
(defn async-leap?
  [year callback]
  (js/setImmediate
   (fn []
     (let [result (and (zero? (js-mod year 4))
                       (pos? (js-mod year 100))
                       (pos? (js-mod year 400)))]
       (callback result)))))
----

The JavaScript function `setImmediate` is used to emulate an asyncrhonous task and the callback
is executed with the result of that predicate.

To test it, we should write a test case using the previously mentioned `async` macro:

[source, clojure]
----
(t/deftest my-async-test
  (t/async done
    (core/async-leap? 1980 (fn [result]
                             (t/is (true? result))
                             (done)))))
----

The `done` function exposed by the `async` macro should be called after the asynchronous
operation is finished and all assertions have run.

It is very important to execute that function only once. If you execute that function twice or
never, may cause some kind of strange behavior like executing some tests more than one time
or skipping execution of some tests.


==== Fixtures

TBD


==== Integrating with CI

Most continuous integration tools and services expect that test scripts you provide
return a standard exit code. But ClojureScript by default only runs the tests and outputs
the results to standard output. So the compiled file can not be used directly with
continuous integration tools because it does not return any return code when something
fails. It always return the return code `0`.

To fix this, we must instruct the _ClojureScript_ test runner that when it finishes with
a failed state it should set `-1` as a return code. This can be done with this
snippet of code:

[source, clojure]
----
(defmethod cljs.test/report [:cljs.test/default :end-run-tests]
  [m]
  (if (cljs.test/successful? m)
    (set! (.-exitCode js/process) 0)
    (set! (.-exitCode js/process) 1)))
----

This code snippet obviously supposes that you are running the tests using *iojs* or
*nodejs*. If you are running your script in another execution environment, you should be
aware of how you can set the exit code in that environment and modify the previous snippet
accordingly.
